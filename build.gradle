plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.5'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'net.researchgate.release' version '2.8.1'
    id 'com.diffplug.spotless' version '6.16.0'
}

group = 'com.wingbank'
version = "$version"

java {
    sourceCompatibility = '17'
}

ext {
    mapstructVersion = '1.5.5.Final'
}

repositories {
    mavenCentral()
    maven {
        credentials {
            username (System.getenv('NEXUS_USR') != null ? System.getenv('NEXUS_USR') : NEXUS_USR)
            password (System.getenv('NEXUS_PSW') != null ? System.getenv('NEXUS_PSW') : NEXUS_PSW)
        }
        url "${System.getenv('NEXUS_URL') != null ? System.getenv('NEXUS_URL') : NEXUS_URL}/repository/maven-public/"
        allowInsecureProtocol = true
    }
}

dependencies {
    // Spring Boot Starters
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    // Database
    implementation 'com.h2database:h2'
    //runtimeOnly("org.postgresql:postgresql")
    implementation 'org.flywaydb:flyway-core'

    // Kafka
    implementation 'org.springframework.kafka:spring-kafka'

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.12.3'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.3'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.3'

    // Swagger/OpenAPI
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.2.0'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.kafka:spring-kafka-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.cloud:spring-cloud-dependencies:2023.0.1'
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

release {
    git {
        requireBranch = 'master'
    }
}

//TODO: Will be apply.
//spotless {
//	java {
//		removeUnusedImports()
//		prettier(['prettier': '2.8.8', 'prettier-plugin-java': '2.2.0'])
//				.config(['parser': 'java', 'tabWidth': 4, 'printWidth': 120])
//	}
//}

task installLocalGitHook(type: Copy) {
    from new File(rootProject.rootDir, 'scripts/pre-commit')
    into { new File(rootProject.rootDir, '.git/hooks')}
    fileMode 0775

}
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
prepareKotlinBuildScriptModel.dependsOn installLocalGitHook